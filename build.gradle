plugins {
    alias libs.plugins.spring.boot apply false
    alias libs.plugins.spring.dependency.management apply false
    alias libs.plugins.hibernate.orm apply false
    alias libs.plugins.graalvm.native apply false
    alias libs.plugins.lombok apply false
    alias libs.plugins.spotless apply false
    alias libs.plugins.gitHooks
}

gitHooks {
    gitHooksDirectory = project.layout.projectDirectory.dir("git-hooks") // or any other path where you put your hooks
    gitDirectory = project.rootProject.layout.projectDirectory.dir(".git")
}


subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply from: rootProject.file('gradle/spotless.gradle')

    group = 'com.example'
    version = '0.0.1-SNAPSHOT'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${libs.versions.spring.boot.get()}"
            mavenBom "org.junit:junit-bom:${libs.versions.junit.bom.get()}"
        }
    }

    dependencies {
        testImplementation(libs.spring.boot.starter.test)
        testImplementation libs.assertj.core
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    configurations.configureEach {
        resolutionStrategy {
            failOnVersionConflict()
            preferProjectModules()
        }
    }
}